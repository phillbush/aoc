#!/usr/bin/awk -f

function min(a, b) {
	return a < b ? a : b
}

function max(a, b) {
	return a > b ? a : b
}

function abs(a) {
	return a < 0 ? -a : a
}

BEGIN {
	FS = ",| -> "
}

$1 == $3 {
	for (i = min($2, $4); i <= max($2, $4); i++)
		if (++floor[$1, i] == 2)
			n++
	next
}

$2 == $4 {
	for (i = min($1, $3); i <= max($1, $3); i++)
		if (++floor[i, $2] == 2)
			n++
	next
}

$3 - $1 == $4 - $2 {
	for (i = 0; i <= abs($3 - $1); i++)
		if (++floor[min($1, $3) + i, min($2, $4) + i] == 2)
			n++
	next
}

abs($3 - $2) == abs($4 - $1) {
	for (i = 0; i <= abs($3 - $1); i++)
		if (++floor[min($1, $3) + i, max($2, $4) - i] == 2)
			n++
	next
}

END {
	print n
}
