#!/usr/bin/awk -f

function mark(nboard, num,    a, b, i, j, n) {
	marked[nboard, num] = 1
	if (!checked[nboard]) {
		a = ++rowcount[nboard, row[nboard, num]]
		b = ++colcount[nboard, col[nboard, num]]
		if (a >= SIZE || b >= SIZE) {
			checked[nboard] = 1
			win++
		}
	}
	if (win != nboards)
		return
	for (i = 1; i <= SIZE; i++) {
		for (j = 1; j <= SIZE; j++) {
			if (!marked[nboard, board[nboard, i, j]]) {
				n += board[nboard, i, j]
			}
		}
	}
	print n * num
	exit
}

BEGIN {
	RS = ""
	FS = ","
	SIZE = 5
}

NR == 1 {
	nnums = NF
	for (i = 1; i <= nnums; i++)
		nums[i] = $i
	FS = " "
	next
}

{
	nboards++
	for (i = 1; i <= SIZE; i++) {
		for (j = 1; j <= SIZE; j++) {
			n = (i - 1) * SIZE + j
			hasnum[nboards, $n] = 1
			row[nboards, $n] = j
			col[nboards, $n] = i
			board[nboards, i, j ] = $n
		}
        }

}

END {
	for (i = 1; i <= nnums; i++) {
		for (j = 1; j <= nboards; j++) {
			if ((j, nums[i]) in hasnum) {
				mark(j, nums[i])
			}
		}
	}
}
