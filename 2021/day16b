#!/usr/bin/awk -f

function btod(s,    n) {
	while (s) {
		n = n * 16 + BTOD[substr(s, 1, 4)]
		s = substr(s, 5)
	}
	return n
}

function max(a, b) {
	return a > b ? a : b
}

function min(a, b) {
	return (a >= 0 && a < b) ? a : b
}

function parse(    type) {
	type = BTOD["0" substr(input, 4, 3)]
	input = substr(input, 7)
	if (type == "4") {
		return literal()
	}
	lentype = substr(input, 1, 1)
	input = substr(input, 2)
	if (lentype == "0")
		return operator0(type)
	return operator1(type)
}

function literal(    start, group, val) {
	for (;;) {
		start = substr(input, 1, 1)
		group = substr(input, 2, 4)
		input = substr(input, 6)
		val = val * 16 + BTOD[group]
		if (!start || start == "0") {
			break
		}
	}
	return val
}

function operator0(type,    len, save, val, a, b) {
	len = btod("0" substr(input, 1, 15))
	save = substr(input, len + 16)
	input = substr(input, 16, len)
	if (type == "0") {
		while (input) {
			val += parse()
		}
	} else if (type == "1") {
		val = 1
		while (input) {
			val *= parse()
		}
	} else if (type == "2") {
		val = -1
		while (input) {
			val = min(val, parse())
		}
	} else if (type == "3") {
		val = 0
		while (input) {
			val = max(val, parse())
		}
	} else if (type == "5") {
		a = parse()
		b = parse()
		val = a > b
	} else if (type == "6") {
		a = parse()
		b = parse()
		val = a < b
	} else if (type == "7") {
		a = parse()
		b = parse()
		val = a == b
	}
	input = save
	return val
}

function operator1(type,    lenbits, len, save, val, a, b) {
	len = btod("0" substr(input, 1, 11))
	input = substr(input, 12)
	if (type == "0") {
		while (len--) {
			val += parse()
		}
	} else if (type == "1") {
		val = 1
		while (len--) {
			val *= parse()
		}
	} else if (type == "2") {
		val = -1
		while (len--) {
			val = min(val, parse())
		}
	} else if (type == "3") {
		val = 0
		while (len--) {
			val = max(val, parse())
		}
	} else if (type == "5") {
		a = parse()
		b = parse()
		val = a > b
	} else if (type == "6") {
		a = parse()
		b = parse()
		val = a < b
	} else if (type == "7") {
		a = parse()
		b = parse()
		val = a == b
	}
	return val
}

BEGIN {
	FS = OFS = ""
	HTOB["0"] = "0000"
	HTOB["1"] = "0001"
	HTOB["2"] = "0010"
	HTOB["3"] = "0011"
	HTOB["4"] = "0100"
	HTOB["5"] = "0101"
	HTOB["6"] = "0110"
	HTOB["7"] = "0111"
	HTOB["8"] = "1000"
	HTOB["9"] = "1001"
	HTOB["A"] = "1010"
	HTOB["B"] = "1011"
	HTOB["C"] = "1100"
	HTOB["D"] = "1101"
	HTOB["E"] = "1110"
	HTOB["F"] = "1111"
	BTOD["0000"] = 0
	BTOD["0001"] = 1
	BTOD["0010"] = 2
	BTOD["0011"] = 3
	BTOD["0100"] = 4
	BTOD["0101"] = 5
	BTOD["0110"] = 6
	BTOD["0111"] = 7
	BTOD["1000"] = 8
	BTOD["1001"] = 9
	BTOD["1010"] = 10
	BTOD["1011"] = 11
	BTOD["1100"] = 12
	BTOD["1101"] = 13
	BTOD["1110"] = 14
	BTOD["1111"] = 15
}

{
	for (i = 1; i <= NF; i++) {
		input = input HTOB[$i]
	}
}

END {
	print parse()
}
