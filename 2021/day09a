#!/usr/bin/awk -f

function m1(x) {
	return (x + 2) % 3
}

function count(x, y,    i, j) {
	i = x - 1
	j = m1(y)
	if ((i < 1 || a[i - 1, j] > a[i, j]) &&
	    (NR < 3 || a[i, m1(j)] > a[i, j]) &&
	    (x == NF || a[x, j] > a[i, j]) &&
	    (a[i, y] > a[i, j])) {
		n += a[i, j]
	}
}

BEGIN {
	FS = ""
}

{
	y = (NR - 1) % 3
	for (x = 0; x <= NF; x++) {
		if (x < NF)
			a[x, y] = $(x + 1) + 1
		if (NR > 1 && x > 0)
			count(x, y)
	}
}

END {
	y = NR % 3
	for (x = 1; x <= NF; x++)
		count(x, y)
	print n
}
