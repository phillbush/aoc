#!/usr/bin/awk -f

function encode(s,    i, n, r) {
	n = length(s)
	for (i = 1; i <= n; i++)
		r = or(r, bit[substr(s, i, 1)])
	return r
}

BEGIN {
	RS = " \\| |\n"
	bit["a"] = 1
	bit["b"] = 2
	bit["c"] = 4
	bit["d"] = 8
	bit["e"] = 16
	bit["f"] = 32
	bit["g"] = 64
}

(NR % 2) {
	delete segs
	delete lens
	delete trans
	for (i = 1; i <= NF; i++)
		segs[length($i), ++lens[length($i)]] = encode($i)
	trans[segs[2,1]] = 1
	trans[segs[3,1]] = 7
	trans[segs[4,1]] = 4
	trans[segs[7,1]] = 8
	for (i = 1; i <= 3; i++) {
		if (or(segs[5,i], segs[4,1]) == segs[7,1])
			trans[segs[5,i]] = 2
		else if (or(segs[5,i], and(segs[7,1], compl(segs[2,1]))) == segs[7,1])
			trans[segs[5,i]] = 3
		else
			trans[segs[5,i]] = 5
		if (or(segs[6,i], segs[2,1]) == segs[7,1])
			trans[segs[6,i]] = 6
		else if (or(segs[6,i], segs[4,1]) == segs[7,1])
			trans[segs[6,i]] = 0
		else
			trans[segs[6,i]] = 9
	}
}

!(NR % 2) {
	x = 0
	for (i = 1; i <= NF; i++)
		x = 10 * x + trans[encode($i)]
	n += x
}

END {
	print n
}
