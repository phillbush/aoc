#!/usr/bin/awk -f

function encode(s,    i, n, r) {
	n = length(s)
	for (i = 1; i <= n; i++)
		r = or(r, bit[substr(s, i, 1)])
	return r
}

BEGIN {
	RS = " \\| |\n"
	bit["a"] = A = 1
	bit["b"] = B = 2
	bit["c"] = C = 4
	bit["d"] = D = 8
	bit["e"] = E = 16
	bit["f"] = F = 32
	bit["g"] = G = 64
}

(NR % 2) {
	delete segs
	delete lens
	for (i = 1; i <= NF; i++)
		segs[length($i), ++lens[length($i)]] = encode($i)
	a[segs[2,1]] = 1
	a[segs[3,1]] = 7
	a[segs[4,1]] = 4
	a[segs[7,1]] = 8
	for (i = 1; i <= 3; i++) {
		a[segs[5,i]] = (or(segs[5,i], segs[4,1]) == segs[7,1])                        \
		             ? 2                                                              \
		             : (or(segs[5,i], and(segs[7,1], compl(segs[2,1]))) == segs[7,1]) \
		             ? 3                                                              \
		             : 5
		a[segs[6,i]] = (or(segs[6,i], segs[2,1]) == segs[7,1]) \
		             ? 6                                       \
		             : (or(segs[6,i], segs[4,1]) == segs[7,1]) \
		             ? 0                                       \
		             : 9
	}
}

!(NR % 2) {
	x = 0
	for (i = 1; i <= NF; i++)
		x = 10 * x + a[encode($i)]
	n += x
}

END {
	print n
}
