#!/usr/bin/awk -f

function filter(array, type, bit,    count, cond, i) {
	if (length(array) > 1) {
		for (i in array) {
			$0 = i
			count[$bit]++
		}
		if (type)
			cond = count[1] >= length(array) / 2
		else
			cond = count[0] <= length(array) / 2
		for (i in array) {
			$0 = i
			if ($bit == (cond ? !type : type)) {
				delete array[i]
			}
		}
	}
}

BEGIN { FS = "" }
      { co2[$0] = oxygen[$0] = 1 }
END   {
	for (i = 1; (length(oxygen) + length(co2)) > 2; i++) {
		filter(oxygen, 1, i)
		filter(co2, 0, i)
	}
	for (value in oxygen) split(value, v1, "")
	for (value in co2)    split(value, v2, "")
	for (i = 1; i <= NF; i++) {
		m *= 2; n *= 2;
		if (v1[i]) m++
		if (v2[i]) n++
	}
	print n * m
}
