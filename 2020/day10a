#!/usr/bin/awk -f

function swap(a, i, j,   t) {
	t = a[i]; a[i] = a[j]; a[j] = t
}

function qsort(a, left, right,   i, last) {
	if (left >= right)
		return
	swap(a, left, left+int((right-left+1)*rand()))
	last = left
	for (i = left+1; i <= right; i++) {
		if (a[i] < a[left])
			swap(a, ++last, i)
	}
	swap(a, left, last)
	qsort(a, left, last-1)
	qsort(a, last+1, right)
}

function jolts(    i, diff) {
	for (i = 0; i < NR; i++)
		diff[a[i + 1] - a[i]]++
	return diff[1] * (diff[3] + 1)
}

    { a[NR] = $0 }
END { qsort(a, 1, NR); print jolts() }
