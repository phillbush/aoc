#!/usr/bin/awk -f

function abs(n) {
	return (n < 0) ? -n : n
}

function step(op, val,    i, n, w, e, s, x, y) {
	if (op == "R") {
		for (i = (val / 90) % 4; i > 0; i--) {
			n = wayp["N"]
			w = wayp["W"]
			e = wayp["E"]
			s = wayp["S"]
			wayp["E"] = n
			wayp["S"] = e
			wayp["W"] = s
			wayp["N"] = w
		}
	} else if (op == "L") {
		for (i = (val / 90) % 4; i > 0; i--) {
			n = wayp["N"]
			w = wayp["W"]
			e = wayp["E"]
			s = wayp["S"]
			wayp["E"] = s
			wayp["S"] = w
			wayp["W"] = n
			wayp["N"] = e
		}
	} else if (op == "F") {
		x = wayp["E"] - wayp["W"]
		y = wayp["N"] - wayp["S"]
		pos["x"] += x * val
		pos["y"] += y * val
	} else {
		wayp[op] += val
	}
}

BEGIN { wayp["E"] = 10; wayp["N"] = 1 }
      { step(substr($0, 1, 1), substr($0, 2)) }
END   { print abs(pos["x"]) + abs(pos["y"]) }
