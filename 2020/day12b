#!/usr/bin/awk -f

function abs(n) {
	return (n < 0) ? -n : n
}

function step(op, val,    i, tmp) {
	if (op == "R") {
		for (i = (val / 90) % 4; i > 0; i--) {
			tmp = wayp["E"]
			wayp["E"] = wayp["N"]
			wayp["N"] = wayp["W"]
			wayp["W"] = wayp["S"]
			wayp["S"] = tmp
		}
	} else if (op == "L") {
		for (i = (val / 90) % 4; i > 0; i--) {
			tmp = wayp["E"]
			wayp["E"] = wayp["S"]
			wayp["S"] = wayp["W"]
			wayp["W"] = wayp["N"]
			wayp["N"] = tmp
		}
	} else if (op == "F") {
		pos["x"] += (wayp["E"] - wayp["W"]) * val
		pos["y"] += (wayp["N"] - wayp["S"]) * val
	} else {
		wayp[op] += val
	}
}

BEGIN { wayp["E"] = 10; wayp["N"] = 1 }
      { step(substr($0, 1, 1), substr($0, 2)) }
END   { print abs(pos["x"]) + abs(pos["y"]) }
