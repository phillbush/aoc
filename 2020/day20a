#!/usr/bin/awk -f
# based on https://github.com/juntuu/advent_of_code_2020

# reverse string
function rev(str,    ret, i) {
	ret = ""
	for (i = length(str); i > 0; i--)
		ret = ret substr(str, i, 1)
	return ret
}

# get side (LEFT or RIGHT) vertical edge of current tile
function col(n,    ret, i) {
	for (i = 1; i <= NF; i++)
		ret = ret substr($i, n, 1)
	return ret
}

# get regular expression matching edges of current tile
function getregexp(    l, r) {
	l = col(LEFT)
	r = col(RIGHT)
	return $1 "|" rev($1) "|" $NF "|" rev($NF) "|" l "|" rev(l) "|" r "|" rev(r)
}

# get product of corner IDs
function getprod (edges,    a, b, m, n, prod) {
	prod = 1
	m = 0
	for (a in edges) {
		n = 0
		for (b in edges) {
			if (a != b && edges[b] ~ edges[a]) {
				n++
			}
			if (n == 4) {
				break
			}
		}
		if (n == 2) {
			prod *= a
			m++
		}
		if (m == 4) {
			break
		}
	}
	return prod
}

# set constants
BEGIN {
	RS = ""
	FS = "\n"
	TILESIZE  = 10  # tile edge size
	LEFT  = 1
	RIGHT = TILESIZE
}

# populate the array of edge regexps
{
	gsub(/\./, "~")         # dots are special characters, replace them with a harmless char
	id = substr($1,6,4)
	sub(/[^\n]*\n/, "")     # remove id line
	edges[id] = getregexp()
}

# print product of corner IDs
END {
	print getprod(edges)
}

# MIT License
#
# Copyright (c) 2021 Juho Eerola
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.
