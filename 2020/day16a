#!/usr/bin/awk -f

function readfield() {
	nfield++
	range[nfield, "name"] = $1
	range[nfield, 1] = $2
	range[nfield, 2] = $3
	range[nfield, 3] = $4
	range[nfield, 4] = $5
}

function inrange(n, j) {
	return (n >= range[j, 1] && n <= range[j, 2]) || (n >= range[j, 3] && n <= range[j, 4])
}

function invalid(    i, j, n, cnt) {
	for (i = 1; i <= nfield; i++) {
		n = 0
		for (j = 1; j <= nfield; j++) {
			if (!inrange($i, j)) {
				if (++n == nfield) {
					return $i
				}
			} else {
				break
			}
		}
	}
	return 0
}

BEGIN                { FS = "(-|\n|,|: | or )" }
$0 == "" || para % 2 { para++ }
!para                { readfield() }
para == 4            { count += invalid() }
END                  { print count }
