#!/usr/bin/awk -f

function addrule(    i, n, s) {
	n = $1
	$1 = ""
	$0 = $0
	gsub("\"", "", $0)
	sub("^ ", "", $0)
	rule[n] = $0
}

function genregex(r,    n, i, a, regex) {
	if (rule[r] ~ /[ab]/)
		return rule[r]
	n = split(rule[r], a, " ")
	for (i = 1; i <= n; i++) {
		if (a[i] == "|")
			regex = regex "|"
		else
			regex = regex genregex(a[i])
	}
	return "(" regex ")"
}

BEGIN               { FS = ":? +" }
!NF && check = 1    { regex = "^" genregex(0) "$" }
!check              { addrule() }
check && $0 ~ regex { sum++ }
END                 { print sum }
