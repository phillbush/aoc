#!/usr/bin/awk -f

function abs(n) {
	return (n < 0) ? -n : n
}

function prod(mod,    ret) {
	ret = 1
	for (n in mod)
		ret *= mod[n]
	return ret
}

function inv(m, n,    i) {
	for (i = 1; i <= n; i++)
		if ((m * i) % n == 1)
			return i
}

function crt(mod, res,    i, p, m, acc) {
	p = prod(mod)
	for (i = 1; i <= NR; i++) {
		m = p / mod[i]
		acc += res[i] * m * inv(m % mod[i], mod[i])
	}
	return acc % p
}

BEGIN {
	FS = "[. ]"
}

{
	mod[NR] = $4
	res[NR] = ($4 - $12 - NR) % $4
}

END {
	print abs(crt(mod, res))
}
