#!/usr/bin/awk -f

function rev(str,    s, a, i, n) {
	n = split(str, a, "")
	for (i = 1; i <= n; i++)
		s = (a[i] == "1" ? "0" : "1") s
	return s
}

function fill(str, size) {
	while (length(str) < size)
		str = str "0" rev(str)
	return substr(str, 1, size)
}

function cksum(str,    s, a, i, n) {
	do {
		s = ""
		n = split(str, a, "")
		for (i = 1; i <= n; i += 2) {
			s = s (a[i] == a[i + 1] ? "1" : "0")
		}
		str = s
	} while (length(str) % 2 == 0)
	return s
}

    { str = $0 }
END { print cksum(fill(str, 272)) }
